# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=tmux-git
pkgver=2.1.263.g5083e93
pkgrel=1
pkgdesc='A terminal multiplexer'
url='https://tmux.github.io/'
arch=('i686' 'x86_64')
license=('BSD')
provides=('tmux')
conflicts=('tmux')
depends=('ncurses' 'libevent')
makedepends=('git' 'ncurses-devel' 'libevent-devel')
source=("${pkgname}"::'git+https://github.com/tmux/tmux.git'
        'msys-platform.patch'
        'no-check-dirs-permission.patch'
        'tmux-ambiguous-width-cjk.patch'
        'tmux-do-not-combine-utf8.patch'
        'tmux-pane-border-ascii.patch')
sha256sums=('SKIP'
            'cf7e0f642d43fc97f7c55be41c693cceab5c891f81fcee687e918075e55cd533'
            '8f1938c9bcb99fba5441e11379a193c17eee582a03c04f1bde8089d58a84f44f'
            '70de4069b87c9e6cb0f8420672aa55546ac6ca5290527a1b6576e0db7d0b1b0b'
            'aa05d74c7078bc622cfba8e16141a01f0c807d5a2708f76d14e40b33ee0afa6e'
            '2d1cde911ae7d29a30eab4ab44b9371283a3418e2706b8c9b13cbe3ae20a473c')

pkgver() {
  cd "${srcdir}"/$pkgname
  git describe --tags | sed -e 's|-|.|g' -e 's|v||g'
}

prepare() {
  cd "${srcdir}"/$pkgname
  patch -p1 -i "${srcdir}"/msys-platform.patch
  patch -p0 -i "${srcdir}"/no-check-dirs-permission.patch
  patch -p1 -i "${srcdir}"/tmux-ambiguous-width-cjk.patch
  patch -p1 -i "${srcdir}"/tmux-do-not-combine-utf8.patch
  patch -p1 -i "${srcdir}"/tmux-pane-border-ascii.patch
  cp osdep-cygwin.c osdep-msys.c

  msg2 "Running autogen.sh"
  ./autogen.sh
}

build() {
  cd "${srcdir}"/$pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    CPPFLAGS="${CPPFLAGS} -I/usr/include/ncursesw -U_XOPEN_SOURCE"
  make
}

package() {
  cd "${srcdir}"/$pkgname
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/licenses/tmux
  install -m 644 COPYING "${pkgdir}"/usr/share/licenses/tmux

  install -d "${pkgdir}"/usr/share/tmux
  install -m 644 examples/* "${pkgdir}"/usr/share/tmux
  install -d "${pkgdir}"/usr/share/vim/vimfiles/syntax
  install -m 644 examples/tmux.vim "${pkgdir}"/usr/share/vim/vimfiles/syntax

  install -d "${pkgdir}"/usr/share/bash-completion/completions/tmux
  mv "${pkgdir}"/usr/share/tmux/bash_completion_tmux.sh "${pkgdir}"/usr/share/bash-completion/completions/tmux
}
